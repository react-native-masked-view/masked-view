def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
  if (project == rootProject) {
    repositories {
      google()
      mavenCentral()
    }

    dependencies {
      classpath 'com.android.tools.build:gradle:4.1.2'
    }
  }
}

apply plugin: 'com.android.library'

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  if (major == 7 && minor >= 3) {
    return true
  }

  return major >= 8
}

android {
  compileSdkVersion safeExtGet('compileSdkVersion', 28)
  
  if(supportsNamespace()){
    namespace = "org.reactnative.maskedview"
    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 16)
    targetSdkVersion safeExtGet('targetSdkVersion', 28)
  }

  sourceSets {
    main {
      java.srcDirs = ['src/main/java']
    }
  }

  lintOptions {
    abortOnError false
    warning 'InvalidPackage'
  }
    buildTypes {
        release {
        }
    }
}

repositories {
  google()
  maven { url "https://jitpack.io" }
  mavenCentral()
  maven {
    // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
    url "$rootDir/../node_modules/react-native/android"
  }
}

dependencies {
  implementation 'com.facebook.react:react-native:+'
}
